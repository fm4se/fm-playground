plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.0'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'com.diffplug.spotless' version '6.25.0'
}

group = 'de.buw'
version = '0.0.1'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation files('lib/org.alloytools.alloy.dist.jar')
	// https://mvnrepository.com/artifact/org.json/json
	implementation 'org.json:json:20240303'

}

tasks.named('test') {
	useJUnitPlatform()
}

// Spotless configuration for code formatting
spotless {
	java {
		target 'src/**/*.java'
		googleJavaFormat('1.21.0')
		removeUnusedImports()
		trimTrailingWhitespace()
		endWithNewline()
	}

	format 'misc', {
		target '*.gradle', '*.md', '.gitignore'
		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
	}
}

// Convenience tasks for development
task formatCode {
	description = 'Format all code using Spotless'
	group = 'formatting'
	dependsOn 'spotlessApply'
}

task checkFormat {
	description = 'Check code formatting using Spotless'
	group = 'verification'
	dependsOn 'spotlessCheck'
}

// Make check task depend on format check
check.dependsOn checkFormat
